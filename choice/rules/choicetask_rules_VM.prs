// Choice Task Rules Visual-Manual Version
// choice_device.py

//Setup Initial WM contents.
(Define 
	Initial_memory_contents (Goal Do choice_task)
)

(Define Parameters
	//(Visual_perceptual_processor Recoding_failure_rate1 Fixed 0.5)
	//(Auditory_perceptual_processor Recoding_failure_rate1 Fixed 0.5)
	(Manual Execution_fluctuation_factor Normal When_used 1.0 0.1)
	(Eye Eccentricity_fluctuation_factor Normal When_used 1.0 0.1)
	(Eye Property_delay_fluctuation_factor Normal When_used 1.0 0.1)

	(Eye Availability Color Quadratic 49 1 0.5 0.1 0.1 0.004)
	(Eye Availability Shape Quadratic 49 1 0.5 0.1 0.1 0.004)
)

(Define Named_location Center_screen 0. 0.)

//***********************************************************************
//             BLOCK AND TRIAL MANAGEMENT RULES
//***********************************************************************

(TRIAL_mfg
If
(
	(Goal Do choice_task)
	(Not (Step ??? ???))
 )
Then
(
	(Add (Step Start Trial))
 )
)


(TRIAL_start_trial
If
(
	(Goal Do choice_task)
	(Step Start Trial)
 )
Then
(
	(Delete (Step Start Trial))
	(Add (Step Waitfor Visual_Fixation_Onset))
 )
)


//*******************************************************************
//              MAIN RULESET
//*******************************************************************

(VIS_visual_Fixation_onset
If
(
	(Goal Do choice_task)
	(Step Waitfor Visual_Fixation_Onset)
	(Visual ?fixation Detection Onset)
	(Motor Ocular Processor Free)
)
Then
(
	(Send_to_motor Ocular Perform Move ?fixation)
	(Add (Tag ?fixation Fixation_Symbol))
	(Delete (Step Waitfor Visual_Fixation_Onset))
	(Add (Step Waitfor Visual_Stimulus_Onset))
))

(VIS_visual_stimulus_onset
If
(
	(Goal Do choice_task)
	(Step Waitfor Visual_Stimulus_Onset)
	(Visual ?visualstimulus Detection Onset)
	(Tag ?fixation Fixation_Symbol)
	(DIFFERENT ?visualstimulus ?fixation)
	(Motor Ocular Preparation Free)
)
Then
(
    (Send_to_motor Ocular Perform Move ?visualstimulus)
	(Add (Tag ?visualstimulus Visual_Stimulus))
	(Delete (Step Waitfor Visual_Stimulus_Onset))
	(Add (Step Waitfor Visual_Stimulus_Color))
))

(VIS_visual_stimulus_color
If
(
	(Goal Do choice_task)
	(Step Waitfor Visual_Stimulus_Color)
	(Tag ?visualstimulus Visual_Stimulus)
	(Visual ?visualstimulus Color ?color)
)
Then
(
	(Add (Tag ?color Visual_Stimulus_Color))
	(Delete (Step Waitfor Visual_Stimulus_Color))
	(Add (Step Make Visual_Response))
))

//******************************************************************************
//             PARALLEL RESPONSE MAPPING RULES
//******************************************************************************

(MAN_make_visualresponse_RED
If
(
	(Goal Do choice_task)
	(Step Make Visual_Response)
	(Tag ??? Visual_Stimulus)
	(Tag Red Visual_Stimulus_Color)
	(Motor Manual Preparation Free)
)
Then
(
	(Send_to_motor Manual Perform Punch U Right Index)
	(Delete (Step Make Visual_Response))
	(Add (Step Cleanup Memory))
))

(MAN_make_visualresponse_GREEN
If
(
	(Goal Do choice_task)
	(Step Make Visual_Response)
	(Tag ??? Visual_Stimulus)
	(Tag Green Visual_Stimulus_Color)
	(Motor Manual Preparation Free)
)
Then
(
	(Send_to_motor Manual Perform Punch I Right Middle)
	(Delete (Step Make Visual_Response))
	(Add (Step Cleanup Memory))
))

(MAN_make_visualresponse_BLUE
If
(
	(Goal Do choice_task)
	(Step Make Visual_Response)
	(Tag ??? Visual_Stimulus)
	(Tag Blue Visual_Stimulus_Color)
	(Motor Manual Preparation Free)
)
Then
(
	(Send_to_motor Manual Perform Punch O Right Ring)
	(Delete (Step Make Visual_Response))
	(Add (Step Cleanup Memory))
))

(MAN_make_visualresponse_YELLOW
If
(
	(Goal Do choice_task)
	(Step Make Visual_Response)
	(Tag ??? Visual_Stimulus)
	(Tag Yellow Visual_Stimulus_Color)
	(Motor Manual Preparation Free)
)
Then
(
	(Send_to_motor Manual Perform Punch P Right Little)
	(Delete (Step Make Visual_Response))
	(Add (Step Cleanup Memory))
))


//*******************************************************************
//              TRIAL CLEANUP RULES
//*******************************************************************


(TRIAL_cleanup_Tags
If (
	(Goal Do choice_task)
	(Step Cleanup Memory)
	(Tag ?x ?y)  ; likely multiple bindings
)
Then (
	(Delete (Tag ?x ?y))
	(Delete (Step Cleanup Memory))
))

